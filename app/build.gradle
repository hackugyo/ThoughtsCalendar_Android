apply plugin: 'com.android.application'

// load release properties
Properties props = new Properties()
if (rootProject.file("release.properties").exists()) {
  props.load(new FileInputStream(rootProject.file("release.properties")))
} else {
  println "Please create release.properties in project root."
}

android {
  compileSdkVersion 22
  buildToolsVersion "22.0.1"

  defaultConfig {
    applicationId "jp.ne.hatena.hackugyo.thoughtscalendar"
    minSdkVersion 14
    targetSdkVersion 22
    versionCode 2
    versionName "1.1"
  }

  productFlavors {
    development {
      applicationId 'jp.ne.hatena.hackugyo.thoughtscalendar'
    }
    staging {
      applicationId 'jp.ne.hatena.hackugyo.thoughtscalendar.staging'
    }
    production {
      applicationId 'jp.ne.hatena.hackugyo.thoughtscalendar'
    }
  }

  signingConfigs {
    release {
      storeFile rootProject.file("t.keystore")
      storePassword props.storePassword
      keyAlias props.keyAlias
      keyPassword props.keyPassword
    }
  }
  buildTypes {
    release {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.release
    }
    debug {
      minifyEnabled false
      applicationIdSuffix ".debug"
    }
  }

  lintOptions {
    checkReleaseBuilds false
    abortOnError false
  }
  packagingOptions {
    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/NOTICE.txt'
  }
}

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])
  compile 'com.android.support:support-v4:22.0.0'
  compile 'com.android.support:appcompat-v7:22.0.0'
  compile 'com.j256.ormlite:ormlite-android:4.48'
  compile 'com.mcxiaoke.volley:library:1.0.+'
  compile group: 'com.google.guava', name: 'guava', version: '12.0'
  compile project(':AndroidBootstrap:AndroidBootstrap')
  // SnappyDB
  compile 'com.snappydb:snappydb-lib:0.5.0'
  compile 'com.esotericsoftware.kryo:kryo:2.24.0'

}
